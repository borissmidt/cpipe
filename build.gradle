plugins {
    id "scala"
    id "application"
    id "maven"
    id "com.github.johnrengelman.shadow" version "2.0.2"
    id "com.github.maiflai.scalatest" version "0.19"
    id "com.dorongold.task-tree" version "1.3"
    id "org.scoverage" version "2.1.0"
}

repositories {
    jcenter()
    maven {
        name = "nexus-releases"
        url "http://nexus.devops.flexpond.net:8081/repository/maven-releases"
    }
}

dependencies {
    compile "org.scala-lang:scala-library:${scalaLibVersion}"

    // Logging
    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "com.typesafe.scala-logging:scala-logging_${scalaLangVersion}:${scalaLoggingVersion}"

    // Dependency Injection
    compile "javax.inject:javax.inject:${javaxInjectVersion}"
    compile "com.google.inject:guice:${guiceVersion}"
    compile "com.typesafe:config:${typesafeVersion}"

    // Cassandra
    compile "TODO:${cassandraVersion}"

    // Test dependencies
    testCompile "org.scalatest:scalatest_${scalaLangVersion}:${scalaTestVersion}"
    testCompile "org.scalacheck:scalacheck_${scalaLangVersion}:${scalaCheckVersion}"
    testCompile "org.mockito:mockito-core:${mockitoCoreVersion}"
    testCompile "org.pegdown:pegdown:${pegdownVersion}"

    scoverage "org.scoverage:scalac-scoverage-plugin_${scalaLangVersion}:${scoverageVersion}"
    scoverage "org.scoverage:scalac-scoverage-runtime_${scalaLangVersion}:${scoverageVersion}"
}

shadowJar {
    baseName = "cpipe2"
    classifier = null
    version = "0.0.1-SNAPSHOT"
    exclude "application.conf", "logback.xml"
    mainClassName = "com.restore.cpipe2.Main"
    mergeServiceFiles()
}

checkScoverage {
    minimumRate = 0.8
}

// Avoid doing a zip and a tar file, we only need the jar file.
assemble.dependsOn.clear()
assemble.dependsOn shadowJar

// Avoid running and compiling tests twice
check.dependsOn.clear()
check.dependsOn checkScoverage
testScoverage.dependsOn.remove(testClasses)
